# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=waybar
pkgver=0.9.3
pkgrel=3
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'gtkmm3'
    'libjsoncpp.so'
    'libinput'
    'libsigc++'
    'fmt'
    'wayland'
    'libdate-tz.so'
    'libspdlog.so'
    'gtk-layer-shell'
    'libpulse'
    'libnl'
    'libappindicator-gtk3'
    'libdbusmenu-gtk3'
    'libmpdclient'
)
makedepends=(
    'cmake'
    'meson'
    'scdoc' # For generating manpages
    'wayland-protocols'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
    "813-fmt-crash.patch"
)
sha256sums=('15cadd05c6a366fd99a92b0f149974c697902c4683c50e2af69f3e59571793e5'
            '9088cc460005719f2718b40698dfca8a9a2a97cf9a6a018106d9f38cf609732d')

prepare() {
    patch -Np1 -i "$srcdir/813-fmt-crash.patch" -d "Waybar-$pkgver"
}

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr --wrap-mode=nofallback build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
