# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.3.1
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=('x86_64')
url="https://github.com/ijl/orjson"
license=('Apache' 'MIT')
depends=('python')
makedepends=('maturin' 'python-pip' 'rustup')
checkdepends=('python-arrow' 'python-pendulum' 'python-psutil' 'python-pytest'
'python-pytz' 'python-xxhash')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('cce6431009d772dc626f8a94d79f6ef3e46b5983cbeab1063f7226575fc4066772c488ff7c05733ddf970badb9c86be18c1624a36202ea34ee457c088379cdee')
b2sums=('1c8d144f69cdd6d74db5a1c4b2950c262a91634675cdda7a6e2fc3cb13f72c9def9d1fa4265b6af99cc9c453abd871eedb1210966b5bdc2e6197705561902555')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  rustup override set nightly-2020-06-09
}

build() {
  cd "$pkgname-$pkgver"
  maturin build --release \
                --strip \
                --manylinux
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgname-$pkgver"
  mkdir -vp testdir
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --root="testdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PIP_CONFIG_FILE='/dev/null'
  pip install --isolated \
              --root="$pkgdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
