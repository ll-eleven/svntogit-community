# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.4
_commit=6124cd71317add93500e0cb04c98cf5606adedea
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://github.com/Zac-HD/hypothesmith/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('cfb56ae43ff93a26fad77658cfb575d8bd79d9103ffc61952c69abfd40f43178ca20f81c936aca32124e12502679ffe35bb1d8f06b72065f645a7306352dba03')

build() {
  cd hypothesmith-$_commit
  python setup.py build
}

check() {
  cd hypothesmith-$_commit
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}
