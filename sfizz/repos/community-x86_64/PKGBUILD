# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=0.4.0
pkgrel=1
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
groups=('lv2-plugins' 'pro-audio' 'vst3-plugins')
# TODO: devendor abseil-cpp
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'xcb-util' 'xcb-util-cursor'
'libxkbcommon' 'libxkbcommon-x11')
makedepends=('cmake' 'jack' 'freetype2' 'libsndfile' 'lv2' 'xcb-util-keysyms')
checkdepends=('lv2lint')
optdepends=('jack: for sfizz_jack')
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz")
sha512sums=('3ebf67e5c407ff56d93ad1f8a59047d5ad2e64bf4d260bbd59fa316e49d6a428d5325979b86fc156ad1174d67af6a7ccbb2fb33b0f8a99a4cd6956de8aa6114b')
b2sums=('e3c0d0d1cc86b75e92c8f8d0f529a54bed24e582738e11d222b5217c32e47be0004b7b943cd72f72afc9034d74286e8ef9fe941f32a808381807807aac2811cf')

prepare() {
  cd "$pkgname-$pkgver"
  # devendor lv2
  rm -rfv lv2/lv2
  sed -e 's|"lv2/core/lv2.h"|<lv2/core/lv2.h>|g' \
      -i lv2/external/ardour/ardour/lv2_extensions.h
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE='Release' \
        -ENABLE_LTO=ON \
        -DSFIZZ_TESTS=ON \
        -DSFIZZ_VST=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  lv2lint -Mpack -I "build/${pkgname}.lv2" "http://sfztools.github.io/sfizz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
