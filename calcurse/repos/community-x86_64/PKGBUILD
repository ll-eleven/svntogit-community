# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: wain <wain.at.gmx.net>

pkgname=calcurse
pkgver=4.6.0
pkgrel=2
pkgdesc='A text-based personal organizer.'
arch=('x86_64')
url='https://calcurse.org/'
license=('BSD')
depends=('ncurses')
optdepends=('python: for CalDAV support' 'python-httplib2: for CalDAV support')
install='calcurse.install'
source=("https://calcurse.org/files/${pkgname}-${pkgver}.tar.gz"{,.asc})
md5sums=('20b59e3a41713f6433be252ea8413dbe'
         'SKIP')
validpgpkeys=(2E36D8620221482FC45CB7F2A91764759326B440) # Lukas Fleischer

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-docs --without-asciidoc --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
