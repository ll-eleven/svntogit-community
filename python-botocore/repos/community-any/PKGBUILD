# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Ainola
# Contributor: Chris Fordham

pkgname=python-botocore
pkgver=1.17.47
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services'
arch=('any')
url="https://github.com/boto/botocore"
license=('Apache')
depends=('python-dateutil' 'python-jmespath' 'python-docutils' 'python-urllib3')
makedepends=('python-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        docutils-0.16.patch)
md5sums=('3eae2585a7e5022ae37dbeb86ef43c64'
         'a7cd91ca093446fa30459a9f43170b29')

prepare() {
  cd botocore-$pkgver
  # https://github.com/boto/botocore/pull/2011
  patch -Np1 -i ../docutils-0.16.patch
}

build() {
  cd botocore-$pkgver
  python setup.py build
}

package() {
  cd botocore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
