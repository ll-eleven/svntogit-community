#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2020.8.19
pkgrel=2
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.8.19.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.8.19.tar.xz.sig
)
sha512sums=(
  32443765b618db1306bb1f35416f146729a5b9bfe5c789bff446fddbc64ee87ec3a9a9b39b9849f7ec48c69f5974aa840a9d10a8b041c6c5f929d7a17fe5e350
  3b5efc133b19178cebf7eb40d31d935de7d48060d446f1c8cd4d5dd87416e8743314943e346f01de1339ef9109045eb83f8c7c9c9968c173027016f8bc4c244c
)
md5sums=(
  eace2053129c8432701134148bd53a6d
  73e94d740dc30e89c5a8d3257cefff51
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
