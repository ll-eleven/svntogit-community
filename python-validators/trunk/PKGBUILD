# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-validators
pkgver=0.18.0
pkgrel=1
pkgdesc="Python Data Validation for Humans"
url="https://github.com/kvesteri/validators"
license=('MIT')
arch=('any')
depends=('python-decorator' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kvesteri/validators/archive/$pkgver.tar.gz")
sha512sums=('abc963857f5a97290111c7be2a5d489ead9f43d8384a8dc51af6cf3fb2269cd998534bb54d0ee97da7f71f79509fdd40712df2716dab92bfca8eaaf3a06ae90e')

build() {
  cd validators-$pkgver
  python setup.py build
}

check() {
  cd validators-$pkgver
  python -m pytest
}

package() {
  cd validators-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
