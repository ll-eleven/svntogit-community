# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemloaders
pkgver=1.0.2
pkgrel=1
pkgdesc="Library to populate items using XPath and CSS with a convenient API"
url="https://github.com/scrapy/itemloaders"
license=('BSD')
arch=('any')
depends=('python-w3lib' 'python-parsel' 'python-jmespath' 'python-itemadapter')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/scrapy/itemloaders/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d55c57b41aba50eadab55d076ac202c70d5c4f535752fc08c7352e2eafca1c0db1be72948da34fad4741b8d0af23d6a71f76f6fb8863871406fdefa77f5cf12a')

build() {
  cd itemloaders-$pkgver
  python setup.py build
}

check() {
  cd itemloaders-$pkgver
  python -m pytest
}

package() {
  cd itemloaders-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
