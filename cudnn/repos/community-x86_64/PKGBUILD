# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=8.0.2.39
_cudaver=11.0
_majorver=8
pkgrel=2
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=11')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/container-images/cuda/-/tree/master/dist for the appropriate cuda version
# or make an NVIDIA Developer account.
source=("https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn${_majorver}-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn${_majorver}_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('e5f2cd07450bc9975d538ad64aa1b034a1d04ba743b0841a3d3d4672df8ad1104a33de1355ae4bdf6b97d6a03eb10af5ad96136a243beae7d473cdb65d6f3697'
            '6d9d048baffec36b4a3ad4a9a6186c1033e9dac828881602dfeb2d0c2f181993fc7add6e2582f8e054f02a82e019419f8e36ebc58b53a7efe1ed08280599f342'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
           "libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb")

package() {
  mkdir -p "${pkgdir}"/usr/{lib,include}

  mkdir libcudnn${_majorver}-dev
  cd libcudnn${_majorver}-dev
  ar xv "${srcdir}"/libcudnn${_majorver}-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/include/x86_64-linux-gnu/* "${pkgdir}"/usr/include/
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  mkdir libcudnn${_majorver}
  cd libcudnn${_majorver}
  ar xv "${srcdir}"/libcudnn${_majorver}_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  # Get rid of some Ubuntu-isms but at the same time still allow for them to
  # work because nobody is ever quite sure what the right names are.
  ln -s cudnn_v${_majorver}.h "${pkgdir}"/usr/include/cudnn.h
  ln -s cudnn_adv_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_adv_infer.h
  ln -s cudnn_adv_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_adv_train.h
  ln -s cudnn_backend_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_backend.h
  ln -s cudnn_cnn_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_cnn_infer.h
  ln -s cudnn_cnn_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_cnn_train.h
  ln -s cudnn_ops_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_ops_infer.h
  ln -s cudnn_ops_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_ops_train.h
  ln -s cudnn_version_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_version.h
  ln -s libcudnn.so.${_majorver} "${pkgdir}"/usr/lib/libcudnn.so
  ln -s libcudnn_static_v${_majorver}.a "${pkgdir}"/usr/lib/libcudnn_static.a

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
