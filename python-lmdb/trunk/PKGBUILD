# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=py-lmdb
pkgname=python-lmdb
pkgver=0.99
pkgrel=1
pkgdesc="Universal Python binding for the LMDB Lightning Database"
arch=(x86_64)
url="https://github.com/jnwatson/py-lmdb/"
license=('custom: OpenLDAP')
depends=(python python-cffi lmdb)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/${_pkg}_${pkgver}.tar.gz)
sha256sums=('d27b90ebda734b65606e51dd6588764a42fb23f381d2f2ea7c899741ac5065d3')

build() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py build
}

check() {
  cd ${_pkg}-${_pkg}_${pkgver}
  local _pyver
  _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
  export LMDB_FORCE_SYSTEM=1
  export PYTHONPATH="$(pwd)/build/lib.linux-${CARCH}-${_pyver}"
  pytest
}

package() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py install --root="$pkgdir" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
