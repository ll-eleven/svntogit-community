# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.6.42
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('ae74553e4ef8b8d6057bf4a9932e5c0732744f6d825bddf39bb0ba745f1dba989341cd5a998ac21ad6242d4115f4cd67fcd68f2ec1e7837bc777462088b4ed47')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i '/#include <DPalette>/a #include <QPainterPath>' src/widgets/{tip,toolbutton}.h
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
