# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=9.0.3
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('243c3fa5421fdbb02ecfea617acad2b6783cef5896ebbde1d07bddfcea5ae9b7557bf8644f6999dc1c81ec66710599a4e9c8c4d43381f6e3cc246b454a177744')
b2sums=('123eb51e629e5806e946ee2ae0f49148e527c85f9eb95331a06cb47f818a5936a7cd0c9b6c070fdb047211314374d7dcb3c9f2c44f3a5d80503236e92be44ac3')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
