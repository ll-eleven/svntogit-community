# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=binaryen
epoch=1
pkgver=96
pkgrel=2
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
makedepends=('cmake' 'ninja' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/WebAssembly/binaryen/archive/version_${pkgver}.tar.gz"
        b43807a835fc878e5eefcb8b4a18aff62d7f4540.patch
        "binaryen.sh")
sha384sums=('36edaa87c8813b04cea7f541b18f0d37c925be9251c3c802a1e2ff9a0be77fe160127355acb97726b19d135afdb4801f'
            '4b3f09e296b8816d2250fbb68b18b45856c728bfd39615d80fe962480c8313dc1898a7814562eed2c82f662acebe7b55'
            '8ccff587d33aa2f8e66cc167ab887087993cea0899921d2e7cc69372c8b659770a4f4a00e9da61c75394af002c8c7660')

prepare() {
    cd binaryen-version_$pkgver
    patch -Np1 -i "$srcdir"/b43807a835fc878e5eefcb8b4a18aff62d7f4540.patch
}

build() {
    cd binaryen-version_$pkgver

    cmake . \
        -Bbuild \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    cd binaryen-version_$pkgver
    DESTDIR="$pkgdir" ninja -C build install

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm755 "$srcdir"/binaryen.sh "$pkgdir"/etc/profile.d/binaryen.sh
}
