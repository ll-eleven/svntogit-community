# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=palp
pkgver=2.12
pkgrel=1
pkgdesc="A Package for analyzing Lattice Polytopes"
arch=(x86_64)
url="http://hep.itp.tuwien.ac.at/~kreuzer/CY/CYpalp.html"
license=(GPL3)
depends=(glibc)
source=("http://hep.itp.tuwien.ac.at/~kreuzer/CY/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('f47ad9317549cb33bb4a098cc53c4af7baff0f86164aa7b4c8b46b4fa09af626')

prepare() {
  cd $pkgname-$pkgver
# Honor system cflags
  sed -e "s|^CFLAGS=.*|CFLAGS=${CPPFLAGS} ${CFLAGS} ${LDFLAGS}|" -i Makefile -i GNUmakefile
}

build() {
  cd $pkgname-$pkgver
  mkdir bin
  mv Global.h Global.h-template
  for dim in 4 5 6 11; do
    sed "s/^#define[^a-zA-Z]*POLY_Dmax.*/#define POLY_Dmax $dim/" Global.h-template > Global.h
    make
    for file in poly class cws nef mori; do
        mv ${file}.x bin/${file}-${dim}d.x
    done
  done
}

package() {
  cd $pkgname-$pkgver
  pushd bin
    for exe in *.x; do
	install -Dm755 $exe -t "$pkgdir"/usr/bin
    done
  popd
  for file in poly class cws nef mori; do
    ln -sf ${file}-6d.x "$pkgdir"/usr/bin/${file}.x
  done
}
