# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-discover
pkgname=haskell-tasty-discover
pkgver=4.2.1
pkgrel=51
pkgdesc="Test discovery for the tasty framework"
url="http://git.coop/lwm/tasty-discover"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-glob')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-tasty' 'haskell-tasty-hedgehog' 'haskell-tasty-hspec'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-smallcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('46dff0398f012d58f53b151ceeea02831149d86d5c4dcc66217c7d53825efadb8dd8226d10d0082ef236763649a39e44c400994a93bd9e7052be7b527f8fa7b7')

build() {
    cd $_hkgname-$pkgver

    # Hack to allow ghc execute tasty-discover when building tests
    export PATH="$PWD/dist/build/$_hkgname:$PATH"
    export LD_LIBRARY_PATH="$PWD/dist/build"

    # Test could be built before executable, so build an executable first
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla
    runhaskell Setup build $MAKEFLAGS

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
