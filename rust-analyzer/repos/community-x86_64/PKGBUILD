# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200824
_pkgver=2020-08-24
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('314cd12b2a2eb30ab3b8f7a8fcafd4d427f5e6513bce1e7258b65621d4fcd40084ef82ac276246a546f50256a67ab2caec2f4d0d2c6b2cc0ca1b5561ac0e522e')

pkgver() {
  echo ${_pkgver//-}
}

prepare() {
  cd $pkgname-$_pkgver
  sed -i '/ensure_rustfmt/d' xtask/src/codegen.rs
  sed -i '/ensure_rustfmt()?;/d' xtask/src/lib.rs
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$_pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
