# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000135
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
depends=('perl-module-pluggable' 'perl-importer' 'perl-scope-guard' 'perl-sub-info'
         'perl-term-table' 'perl-test-simple>=1.302176')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('6178d6ae5f7981de70ef245143bf990587dd35423521feb0155d9294507ee78e070586fe205e8141f7a0c46d9928fdccc94601fd7a21cc508d2c13f7ff4f9871')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
