# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=5.8.0.5.20200819
_commit=a66b118ba2046dc65dbfa0b4942de6138bb45ba1
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-icon-theme' 'deepin-sound-theme')
makedepends=('python' 'go' 'golang-deepin-lib')
conflicts=('deepin-artwork-themes')
replaces=('deepin-artwork-themes')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$_commit.tar.gz"
        https://github.com/linuxdeepin/deepin-desktop-schemas/commit/bf0c4e43f6b6d508ddd346c2d1e865dae9ae947d.patch)
sha512sums=('c17d70e896fa7e3e3c624df4c26af587766efb21b2a2b76bf0f7cf8a721322e48bfe55f593d01fc1fe564493f5b7dd08946cc2b5c59619d6d94752df03cddeae'
            'be13e501baf0517da19618011219b53d633a4186840b20b24d134e5d667c4ab1b6b716c09c78faf802b32ecf3f6f6e5e2f84744a5919b28645f002739d07ea82')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  cd deepin-desktop-schemas-$_commit
  # disable swap-sched
  patch -Rp1 -i ../bf0c4e43f6b6d508ddd346c2d1e865dae9ae947d.patch
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/common/com.deepin.wrap.gnome.desktop.override
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd deepin-desktop-schemas-$_commit
  make ARCH=x86
}

check() {
  cd deepin-desktop-schemas-$_commit
  make test
}

package() {
  cd deepin-desktop-schemas-$_commit
  make DESTDIR="$pkgdir" install
}
