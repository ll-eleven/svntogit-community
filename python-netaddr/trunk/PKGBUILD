# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien LEDUC <sebastien@sleduc.fr>
# Contributor: Gilles CHAUVIN <gcnweb at gmail dot com>

pkgname=python-netaddr
_pyname=netaddr
pkgver=0.7.20
pkgrel=1
pkgdesc='Pure Python network address representation and manipulation library'
url='https://github.com/drkjam/netaddr'
arch=('any')
license=('BSD')
depends=('python')
makedepends=('python-sphinx')
checkdepends=('python-pytest' 'python-pytest-cov')
optdepends=('ipython: netaddr interractive command support')
options=('!makeflags')
source=(https://github.com/drkjam/${_pyname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e5f99aad04bfc5dee3531059fdbbc1f4910ef880a12c0591bf8ff8fa55fecb666087432c2e74a2d9215da9f8e2848f0096fa966f3496f5d87558f0421ff1a429')
b2sums=('c06ae455851db0b344fbfbce8280e3f6a22b1b5b70933415425f45d5702e875668e1424c5726c11c4f88195b1a11957c2c13eb8c626ec2f935faa22312d4c4f9')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -r 's|#!/usr/bin/env python||g' -i netaddr/eui/ieee.py netaddr/ip/iana.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -C docs man text
}

check() {
  cd ${_pyname}-${pkgver}
  LC_ALL='en_US.UTF-8' py.test
}

package_python-netaddr() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/build/man/netaddr.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
