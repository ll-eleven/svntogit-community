# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.7
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-update-tests-1.patch::https://github.com/lilydjwg/nvchecker/commit/44687da8a6f4e3bdeb18baf1132518b17bddd484.patch"
        "update-tests-2.diff")
sha512sums=('6243960747de33bd004bb3377109699f702bb64d65320e0c219f540546f33ace2d87be4eb2b18d988874eb98812176488fa5c98aec6f93cd218b8549b38482d0'
            '24d14870de13bbb2beec128854cfb04a65e3fee7720f62856c4c20afadf83975b0d3e26888a96f3663218556ddd0e173831b4c9c89305cacf8f94e531025d0e0'
            'c9898cef468463f8c1374c44e65e05988d16b9051fc6ec9d06e7263d21069225bef0f7ce2bfff5600bcef91fac6babe20a5da7a6c9169c5d386ba42900317b74')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-update-tests-1.patch
  # Backported from https://github.com/lilydjwg/nvchecker/commit/649799389f97d7f80cb9ba8f3fb3d9742bb7f174
  patch -Np1 -i ../update-tests-2.diff
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
