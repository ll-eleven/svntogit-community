# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: snuo

pkgname=extundelete
pkgver=0.2.4
pkgrel=5
pkgdesc="Utility for recovering deleted files from ext2, ext3 or ext4 partitions by parsing the journal"
arch=('x86_64')
url="http://extundelete.sourceforge.net"
license=('GPL')
depends=('e2fsprogs')
changelog=${pkgname}.changelog
source=(https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        extundelete-e2fsprogs-1.44.1.patch)
sha256sums=('a1f9dd61247056d36401ce5d6785e74d08a184340eebd3865c345ddaa93f19f4'
            '944a1dc5a0697334fa27eabc198a6b230740199a01d3a942c01570edb996426f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../extundelete-e2fsprogs-1.44.1.patch # Fix build with e2fsprogs 1.44.1
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
