# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Carlo Cabanilla <carlo.cabanilla@gmail.com>
# Contributor: Christoph Gysin <christoph.gysin@gmail.com>

pkgname=aws-vault
pkgver=6.1.0
pkgrel=1
pkgdesc='Vault for securely storing and accessing AWS credentials in development environments'
url='https://github.com/99designs/aws-vault'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('go')
optdepends=(
  'zenity: graphical prompt'
  'kdialog: graphical prompt'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('be4e5970643e586c24ec1246d21a82ce0ef0624a4b7585d49078aca21e459930')
b2sums=('6416adbfb186c55c1811095e16e9e759e6d092bf8477537e4e53d7c266ae57da289941f0f93353daef5d94d64c885c987581344a339bd9e7c0defbe331ae8e71')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X main.Version=v${pkgver}" .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md USAGE.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 -p contrib/completions/bash/${pkgname}.bash "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm 644 -p contrib/completions/zsh/${pkgname}.zsh "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm 644 -p contrib/completions/fish/${pkgname}.fish "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
