  # $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-qt
pkgver=0.0.0.20200822
_commit=73bfd76dd7ad902856aa07b27c19a51301ab7377
pkgrel=1
pkgdesc="Fcitx5 Qt Library"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-qt"
license=('GPL')
groups=('fcitx5-im')
depends=('fcitx5' 'qt5-base')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://github.com/fcitx/fcitx5-qt/archive/$_commit/fcitx5-qt-$_commit.tar.gz")
sha512sums=('16ac6dcfd2f984cb8d051db536c1b90b2536c8c41b368aff9a5d709f063f8abbbd220d0c0a896c68bb038a5e2e6bed6d2ef7d94bb38a4fff4bdd7837aa0ebe06')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DENABLE_QT4=0 .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
