# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-colorlog
pkgver=4.2.1
pkgrel=1
pkgdesc='A colored formatter for the python logging module'
arch=('any')
url='https://github.com/borntyping/python-colorlog'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('169169a06869ac736b0509dea2b9bbe99edb0e8cc66d2da93d878ef31dc2fb5cec77afa53994a8e22f43b226bf67b0458e2ef5ea99dd9d96ac393909091b5d98')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
