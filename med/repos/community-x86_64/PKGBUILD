# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guilhem Saurel <guilhem@saurel.me>
# Contributor: Michel Zou
# Contributor: Oliver Goethel

pkgname=med
pkgver=4.0.0
pkgrel=8
pkgdesc='Generic pre- and post-processing platform for numerical simulation'
url="https://www.salome-platform.org/downloads"
license=(LGPL)
arch=(x86_64)
depends=(hdf5 python tk)
makedepends=(cmake gcc-fortran ninja swig)
source=("https://files.salome-platform.org/Salome/other/$pkgname-$pkgver.tar.gz"
        'https://gist.githubusercontent.com/jedbrown/527ef81ff59a0dccf833da40fdd15a47/raw/7482f9a42fdd277f5f9cf0167ea7a3381032ca7a/hdf5-1.12.patch'
        pycompile.patch)
sha256sums=('a474e90b5882ce69c5e9f66f6359c53b8b73eb448c5f631fa96e8cd2c14df004'
            '82d28b6ae2ee6ceb3c6de9acb861a49741e2e759c7c556217f7d284216788206'
            'fec659efc391376eeb73c2e749d98ae504a178dde2d2e9d7b81d26537a043300')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../hdf5-1.12.patch
  patch -p1 -i ../pycompile.patch
}

build() {
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMEDFILE_BUILD_PYTHON=ON \
    -DMEDFILE_BUILD_TESTS=OFF \
    -DMEDFILE_INSTALL_DOC=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
