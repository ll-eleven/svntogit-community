# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

_name=pytesseract
pkgname=python-pytesseract
pkgver=0.3.5
pkgrel=1
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-pillow' 'python-setuptools' 'tesseract')
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("$url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('8a1f3dc1378726c7d6376ea8b42d222d907d83c2db9e84da3cc0add5d70e7023bffcde087e413f6d3be09c78929f9b8552f69b8c33608b9772362f8289d1d003')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
