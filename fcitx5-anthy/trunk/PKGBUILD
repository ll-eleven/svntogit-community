# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=0.0.0.20200728
_commit=4a31821e6105f9121f1ae1880e2ee6b576fa500a
pkgrel=2
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL')
depends=('fcitx5' 'anthy')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://github.com/fcitx/fcitx5-anthy/archive/$_commit/fcitx5-anthy-$_commit.tar.gz")
sha512sums=('66748bf56e012bc44a847e616e6307c20838da3969c23739aca704e995728da7e70e7dcffed0c4467f6763feb5175c9ed860affb53c4a6b1605c2e99f05ea7fd')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
