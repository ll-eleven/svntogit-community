# Maintainer: David Runge <dvzrv@archlinux.org>

# weird name on pypi, because reasons
# _name=djangorestframework
_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.11.1
pkgrel=1
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=('any')
url="https://www.django-rest-framework.org/"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-django')
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=('python-django-filter: for dynamic queryset filters'
            'python-django-guardian: for a per-object permission model'
            'python-markdown: for markdown support'
            'python-psycopg2: for PostgreSQL support'
            'python-pygments: for syntax highlighting'
            'python-yaml: for YAML support')
# tests not in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('36382146164d1ab1284adb55a46ec2114b27d22d9a6de57d6b1789ae0295399d6be1cfa0e1f7047f866ba6c815e210efd6c8c13170ffa8040ad016f9835d932a')
b2sums=('068e4a3b28939f8792653abe89099d6778b420bb97c49c1f7712beff028f5e0caa0e773c1ac60528fb164a4596ceee8d39f1690d9a43f00732ae0d418020e82d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
