# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=python-sh
pkgver=1.14.0
pkgrel=1
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=('any')
url='https://pypi.python.org/pypi/sh'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'lsof')
source=("https://pypi.io/packages/source/s/sh/sh-$pkgver.tar.gz")
sha512sums=('816c0562f4f779ba79b5206d151893cd5043898d1b74ee109c0ea67f862a097ab9c7509e4a20459fc623f14fa3fb1a9e2a2ccec8257a68fcba14414779a20128')

prepare() {
  cd sh-$pkgver
  sed -i 's|/bin/sleep|/usr/bin/sleep|' test.py
}

build() {
  cd sh-$pkgver
  python setup.py build
}

check() {
  cd sh-$pkgver
  python sh.py travis
}

package() {
  cd sh-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/python-sh/LICENSE
}

# vim:set ts=2 sw=2 et:
