# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=LibCST
pkgname=python-libcst
pkgver=0.3.9
pkgrel=1
pkgdesc="A concrete syntax tree parser and serializer library for Python that preserves many aspects of Python's abstract syntax tree"
arch=('any')
url='https://github.com/Instagram/LibCST'
license=('MIT')
depends=('python-typing_extensions' 'python-typing_inspect' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-isort' 'python-black')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6d67205bd874d8c37ac3c15af996b9786dfaf90591a9d38e1de28836708cdb4b0851ac5bcf77352e8ddabcd9f650dfbf90cbf43bc9a6908fb2be8612a0b927ce')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest --addopts '--ignore libcst/tests/test_fuzz.py'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
