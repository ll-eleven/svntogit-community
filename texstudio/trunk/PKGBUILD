# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=3.0.0
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip' 'desktop-file-utils')
makedepends=('qt5-tools' 'mercurial' 'imagemagick' 'librsvg')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs56433.patch)
sha512sums=('d395b71aac58aa9d67430858506877d8344e49c6f1a67a635d53772604d3eb07bb96967bcd6a4e3e0c06fd72fccef5d16b5fff68a2fd6ea68ffcdd6c1199063d'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            '1b7e67e42c9d95f4c89aa8e8f7406df7ad94124415c34ad526d5095c9ad76368357ea3d0df83b356b3bdb86949544d3fcc15845bc2106883800985d36b56d23c')

prepare() {
    cd texstudio-${pkgver}/src
    patch -Np1 < "${srcdir}"/fs54269.patch
    patch -Np1 < "${srcdir}"/fs56433.patch
}

build() {
    cd texstudio-${pkgver}
    qmake-qt5 CONFIG-="debug" USE_SYSTEM_QUAZIP=1 USE_SYSTEM_HUNSPELL=1 texstudio.pro
    make
}

package() {
    cd texstudio-${pkgver}
    make INSTALL_ROOT="${pkgdir}" install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done
}
