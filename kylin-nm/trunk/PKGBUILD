# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kylin-nm
pkgver=3.0.0
pkgrel=1
pkgdesc="Kylin network monitor used in ubuntu-kylin operation system"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/kylin-nm"
groups=('ukui')
depends=('gsettings-qt' 'kwindowsystem' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/kylin-nm/archive/v$pkgver.tar.gz")
sha512sums=('1c20e4ad9786b0f492ecb43938a2db5298f396ecefb156b7276fb8dd97ac19b2b2e92ada20d4feb24b0081e94be45e3cdec4bde87941253d3c6370ab377f1f7c')

prepare() {
  cd kylin-nm-$pkgver
  sed -i '/dpkg-buildflags/d' kylin-nm.pro
}

build() {
  cd kylin-nm-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd kylin-nm-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
