# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.0.2
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gtk2' 'gsettings-qt' 'kwindowsystem' 'libnotify' 'poppler-qt5' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('7e3d4af9b945c6f88c131ffbe34e69814126ef89cc8fd8572245c506a3e3649ccc6c146dfb22c4fd492fd75ed6aa047cd25ac809141e308a47f280bb6aecea3b')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
