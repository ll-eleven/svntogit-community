# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: shadyabhi <abhijeet.1989@gmail.com>

pkgname=python-google-api-python-client
pkgver=1.10.0
pkgrel=1
pkgdesc="Google API Client Library for Python"
arch=('any')
url="https://github.com/google/google-api-python-client"
license=('Apache')
depends=('python-google-auth-httplib2' 'python-uritemplate' 'python-google-api-core')
optdepends=('python-oauth2client: optional backend')
makedepends=('python-setuptools')
source=("google-api-python-client-${pkgver}.tar.gz::https://github.com/google/google-api-python-client/archive/v${pkgver}.tar.gz")
sha256sums=('13d556caed3a270dbe5fc8bba07dcd674f85c7412e52887f1cd04dc9248796a7')

build() {
  cd "$srcdir"/google-api-python-client-${pkgver}
  python setup.py build
}
 
package() {
  cd "$srcdir/google-api-python-client-$pkgver"
 
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  # Workaround for FS#47243
  _site_packages=`python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod -R a+r "$pkgdir$_site_packages"
}
