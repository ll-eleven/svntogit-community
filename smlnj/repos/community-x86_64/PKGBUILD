# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.98
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
depends=(lib32-glibc)
provides=(sml)
install=smlnj.install
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.amd64-unix.tgz::${_url}boot.amd64-unix.tgz"
  "$pkgname-$pkgver-boot.x64-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('39e460e94063b3c85b944c86d29c91ce3995e6fab242a2f77c71d7b04c8e7a7a'
            '1451adf5b9230aaf74d1d28438e4f8126dc56f333bb00b0fc52ff1b596a35281'
            '2ab4a5dff211d84359a50c5d48d607bd4c4f221fee65ac0bcf982f4508692e11'
            'f02776bbe417d48a469abd8d3496ce87e6cd7fd8440101cb23ce3b28b8b0fba3'
            'd7a7e6f9e32af242b3a600fd75830b923698db8f4d2318a7cc67512b01663344'
            '36e9fff81baecf8f770efa20b5e56e6f67e71705d8692142d53a590c133183ba'
            '555950477c86c0c43b3b6bb7ef907d7690cd214fb19c09a1a735ec9be1dbfa1e'
            '208fb9c8cbcfc1c90ab14267e28038a390200002e2bcf3ee5670dcb643ceda11'
            '63d25c1c38791ae6d8f34cfcff600bc4c4ad6a166adb8af6402105bd98e444ba'
            '17c4254b7c1e696591932bc46f1b390c05174b26927fdbab0fa6b0fd9b7baa1a'
            'fb6c671d071c31e50e6792fb015492d8aec6f12b0cea2cd00e0b303c8ce1d98f'
            '7ba1063146e419e1ecebfd420b45eeb641bb8f79802cfc45a0c7e3958ad59e4f'
            '4055ea0bb388893a58453ff8588e1de72a15be94257715bcd66e7a870c08c3d8'
            'e47e0144547a04aaa2d4ee63bafb6a40a294e7acc8e072cc9569b9a08942ca86'
            'cf2c6cdbfae352585a59214d2e6a377191504334bf4dfc092e3a0e64667124a1'
            'bd84ce5671fd1b1156f6779f81dfaabf0801e0c3f476f36e2f2fdb4311d496aa'
            'e494dc37e39bcdc1e506d5b3b31422488dfd0400184ca5fb7005438253d7fa17'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

build() {
  mkdir -p install
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  export CFLAGS="$CFLAGS -Wl,-z,relro,-z-now"
  export LDFLAGS="$LDFLAGS -Wl,-z,relro,-z-now"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh -default 64
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
