# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.27.0
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('dkms' 'jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc')
makedepends=('cmake' 'pandoc' 'gtest')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "bashcomp-location.patch"
        "glibc.patch")
sha256sums=('a67f97b2620e3d9c5d48d07932604c938a5a6d3b625d7a23bfb2eb9802024b52'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b'
            '9d9ee715500cb5c3709cf6a77e95421edaaae5f15dbb1c7c8ad2147cc9637939')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/glibc.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build

  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"
}
