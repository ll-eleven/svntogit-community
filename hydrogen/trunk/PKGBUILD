# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Rob Til Freedmen <rob.til.freedman@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=hydrogen
pkgver=1.0.0
pkgrel=1
pkgdesc="An advanced drum machine"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
url="http://www.hydrogen-music.org/"
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base' 'qt5-xmlpatterns')
makedepends=('alsa-lib' 'cmake' 'jack' 'ladspa' 'libarchive' 'liblo' 'liblrdf'
'libpulse' 'libsndfile' 'portaudio' 'portmidi' 'python' 'qt5-tools')
checkdepends=('cppunit')
optdepends=('new-session-manager: for session management')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}-music/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-1.0.0-i18n_install_path.patch")
sha512sums=('2c5d8e5c6e3a64ce6c11fcfe056719e36618c4e3562513a20545562e5704fdd67bde033ebaf452f497e196186249dd5e0772f8b329a67b4a25f3d9457d3a94ed'
            'b81ea8b1c478df8647a33884627912b70ae183bb445a32d8eefb628da013e1a480b2027688c1edc6f131a6e7a75880c6a375df36e93459d86b07d223043fd15f')
b2sums=('154bef75422ef419f67022e6947bcba5a2a428cfd34463ef65cd021d9503e55d6898c846472ae1c4fc156db031fbfa78e73f8f0b4a55af9e6b1632888d30ff23'
        'd9af01f946a5d4a5e786560cd95d004a5ecb4902e922ee814ef65a484cf2fd6b4e9568d23cdb9f85231e7572624c4a53096dd46494e47dd33248be8c06bfc605')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix i18n installation destination
  # https://github.com/hydrogen-music/hydrogen/pull/912
  patch -Np1 -i "../${pkgname}-1.0.0-i18n_install_path.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DWANT_DEBUG=OFF \
        -DWANT_CPPUNIT=ON \
        -DWANT_LRDF=ON \
        -DWANT_PORTAUDIO=ON \
        -DWANT_PORTMIDI=ON \
        -DWANT_PULSEAUDIO=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
  # update translations
  (
    cd data/i18n
    ./updateTranslations.sh
  )
}

package() {
  depends+=('libarchive.so' 'libasound.so' 'liblo.so' 'libjack.so' 'liblrdf.so'
  'liblo.so' 'libportaudio.so' 'libportmidi.so' 'libpulse.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  # docs
  install -vDm 644 {ChangeLog,DEVELOPERS,INSTALL.md,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
