# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-ant-xml
pkgname=haskell-tasty-ant-xml
pkgver=1.1.6
pkgrel=17
pkgdesc="Render tasty output to XML for Jenkins"
url="https://github.com/ocharles/tasty-ant-xml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-generic-deriving' 'haskell-tagged' 'haskell-tasty' 'haskell-xml')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e4a2d665d05acfd4e996a0568ccaacf61db4452e3720701874b1469ff39ef1f9646ab91780633c7c18bdf53bad28e72a63aa2a1cb82cbc879bd3e382fe9099ff')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *1.3/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
