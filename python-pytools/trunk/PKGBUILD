# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pytools
pkgver=2020.4
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('66dea0d67c99c152edd7cd44e1262b6d163f065f5a4b697a5997f7453145af8fb0539d8eafa606fa18128908ce933c724b51bd1a76a6f7ab02e27db24496dc4c')

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest
}

package(){
  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
